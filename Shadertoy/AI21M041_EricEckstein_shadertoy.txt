// Rotation matrix around the X axis.
mat3 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(1, 0, 0),
        vec3(0, c, -s),
        vec3(0, s, c)
    );
}

// Rotation matrix around the Y axis.
mat3 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, 0, s),
        vec3(0, 1, 0),
        vec3(-s, 0, c)
    );
}

// Rotation matrix around the Z axis.
mat3 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, -s, 0),
        vec3(s, c, 0),
        vec3(0, 0, 1)
    );
}

const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;

struct Surface {
    float sDistanceValue; // signed distance value
    vec3 color; // color
};

Surface sdBox( vec3 p, vec3 b, vec3 offset, mat3 transform)
{
  p = (p - offset) * transform;
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  vec3 col = vec3(mod(floor(p.x * 0.1)  + floor(p.y * 0.1) + floor(p.z * 0.1), 2.0));
  return Surface(d, col); 
}

Surface sdFloor(vec3 p, vec3 col) {
  float d = p.y + 1.;
  return Surface(d, col);
}

Surface minWithColor(Surface obj1, Surface obj2) {
  if (obj2.sDistanceValue < obj1.sDistanceValue) return obj2;
  return obj1;
}

Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.8*mod(floor(p.x) + floor(p.z), 2.0));

  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), rotateY(iTime)* rotateX(iTime)*rotateX(iTime) ));
  return co;
}

Surface rayMarch(vec3 rayOrigin, vec3 rayDirection, float start, float end) {
  float depth = start;
  Surface closestObject;

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 point = rayOrigin + depth * rayDirection;
    closestObject = sdScene(point);
    depth += closestObject.sDistanceValue;
    if (closestObject.sDistanceValue < PRECISION || depth > end) break;
  }
  
  closestObject.sDistanceValue = depth;
  
  return closestObject;
}

vec3 calcNormal(in vec3 p) {
    vec2 e = vec2(1.0, -1.0) * 0.0005; 
    return normalize(
      e.xyy * sdScene(p + e.xyy).sDistanceValue +
      e.yyx * sdScene(p + e.yyx).sDistanceValue +
      e.yxy * sdScene(p + e.yxy).sDistanceValue +
      e.xxx * sdScene(p + e.xxx).sDistanceValue);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = vec3(0.835, 1, 1);

  vec3 col = vec3(0); 
  
  vec3 rayOrigin = vec3(0, 3, 9); // ray origin that represents camera position
  vec3 rayDirection = normalize(vec3(uv, -1)); // ray direction

  Surface closestObj = rayMarch(rayOrigin, rayDirection, MIN_DIST, MAX_DIST); // closest object

  if (closestObj.sDistanceValue > MAX_DIST) {
    col = backgroundColor; // ray didn't hit anything
  } else {
    vec3 p = rayOrigin + rayDirection * closestObj.sDistanceValue; // point on cube or floor we discovered from ray marching
    vec3 normal = calcNormal(p);
    vec3 lightPosition = vec3(0, 0, 6) * rotateY(iTime);
    vec3 lightDirection = normalize(lightPosition - p);

    float dif = clamp(dot(normal, lightDirection), 0.3, 1.); // diffuse reflection

    col = dif * closestObj.color + backgroundColor * .2; // Add a bit of background color to the diffuse color
  }

  // Output to screen
  fragColor = vec4(col, 1.0);
}
